
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)

	ТабДок = Новый ТабличныйДокумент; 
	Печать(ТабДок, ПараметрКоманды);  
	
	ТабДок.Показать("ПечатьОтгрузки");

КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура Печать(ТабДок, Документ) 

	Макет = Документы.РеализацияТоваровИУслуг.ПолучитьМакет("ПечатьОтгрузки");
	
	ОблШапка 		      = Макет.ПолучитьОбласть("Шапка");  
	ОблШапкаНоменклатуры  = Макет.ПолучитьОбласть("ШапкаНоменклатуры");         
	ОблНоменклатура 	  = Макет.ПолучитьОбласть("Номенклатура");
	ОблПодвал 			  = Макет.ПолучитьОбласть("Подвал"); 
		 
	
	// шапка
	Если Документ.Контрагент.ТипЛица = Перечисления.ТипЛица.ФизЛицо Тогда
		ОблШапка.Параметры.НазваниеДокумента = "Товарный чек";	
	Иначе
		ОблШапка.Параметры.НазваниеДокумента = "Торг-12";
	КонецЕсли;
	
	ОблШапка.Параметры.НомерДокумента	 = Документ.Номер;  
	
	ТабДок.Вывести(ОблШапка); 
	
	// шапка номенклатуры
	ТабДок.Вывести(ОблШапкаНоменклатуры);

	
	// Номенклатура 	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РеализацияТоваровИУслугТовары.НомерСтроки КАК НомерСтроки,
		|	РеализацияТоваровИУслугТовары.Номенклатура КАК Номенклатура,
		|	РеализацияТоваровИУслугТовары.Количество КАК Количество,
		|	РеализацияТоваровИУслугТовары.Цена КАК Цена,
		|	РеализацияТоваровИУслугТовары.ЕдИзмерения КАК ЕдИзмерения,
		|	РеализацияТоваровИУслугТовары.Сумма КАК Сумма,
		|	РеализацияТоваровИУслугТовары.СтепеньБрака КАК СтепеньБрака
		|ИЗ
		|	Документ.РеализацияТоваровИУслуг.Товары КАК РеализацияТоваровИУслугТовары
		|ГДЕ
		|	РеализацияТоваровИУслугТовары.Ссылка = &Документ";
	
	Запрос.УстановитьПараметр("Документ", Документ);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл

		ОблНоменклатура.Параметры.НомерСтроки    = Выборка.НомерСтроки;
		ОблНоменклатура.Параметры.Номенклатура   = Выборка.Номенклатура;
		ОблНоменклатура.Параметры.Количество 	 = Выборка.Количество;
		ОблНоменклатура.Параметры.ЕдИзм 		 = Выборка.ЕдИзмерения;
		ОблНоменклатура.Параметры.Цена 			 = Выборка.Цена;
		ОблНоменклатура.Параметры.Сумма 		 = Выборка.Сумма;
		ОблНоменклатура.Параметры.СтепеньБрака	 = Выборка.СтепеньБрака;
		
		ТабДок.Вывести(ОблНоменклатура); 

	КонецЦикла;
	
	// подвал
	ОблПодвал.Параметры.ПодписьОрганизации = ПараметрыСеанса.ПользовательСеанса;
	ОблПодвал.Параметры.ПодписьКлиента	   = Документ.КонтактноеЛицо.ФИО;   
	ОблПодвал.Параметры.СуммаДокумента	   = ЧислоПрописью(Документ.СуммаДокумента, "Л=ru_RU;ДП=Истина", "рубль,рубля,рублей,м,копейка,копейки,копеек,ж,2"); 
	
	ТабДок.Вывести(ОблПодвал);
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Для Каждого Док Из Метаданные.Документы Цикл
		
		Элементы.МетаДокумент.СписокВыбора.Добавить(Док.Имя, Док.Синоним);
		
	КонецЦикла;     	
		
КонецПроцедуры


&НаКлиенте
Процедура МетаДокументПриИзменении(Элемент)
	
	ЗаполнитьФормыНаСервере(Объект.МетаДокумент);	

КонецПроцедуры


&НаСервере
Процедура ЗаполнитьФормыНаСервере(ИмяДока)

	СвойстваДока = Метаданные.Документы.Найти(ИмяДока);  
	
	ДеревоЗн = Новый ДеревоЗначений;  
	
	ДеревоЗн.Колонки.Добавить("Имя");
	ДеревоЗн.Колонки.Добавить("Синоним"); 
	
	Строка 	   = ДеревоЗн.Строки.Добавить();
	Строка.Имя = Строка(ИмяДока);   
	
	
	// Стандартные реквизиты	
	ДобавитьЭлементыВДеревоЗначений(СвойстваДока.СтандартныеРеквизиты, 
	Строка, 
	"Стандатные реквизиты");	
	
	
	// Реквизиты   
	ДобавитьЭлементыВДеревоЗначений(СвойстваДока.Реквизиты, 
	Строка, 
	"Реквизиты");	
	
	
	// Табличные части
	СписокТЧ 	 = Строка.Строки.Добавить();
	СписокТЧ.Имя = "Табличные части";   
	
	Для Каждого ТЧ Из СвойстваДока.ТабличныеЧасти Цикл
		
		СтрТЧ 	      = СписокТЧ.Строки.Добавить();
		СтрТЧ.Имя 	  = ТЧ.Имя;
		СтрТЧ.Синоним = ТЧ.Синоним;
		
		Для Каждого Реквизит Из ТЧ.Реквизиты Цикл
		
			СтрРеквизитаТЧ 		   = СтрТЧ.Строки.Добавить();
			СтрРеквизитаТЧ.Имя     = Реквизит.Имя;
			СтрРеквизитаТЧ.Синоним = Реквизит.Синоним;
			
		КонецЦикла;
		
	КонецЦикла;  
	
	
	// Формы   
	ДобавитьЭлементыВДеревоЗначений(СвойстваДока.Формы, 
	Строка, 
	"Формы");
	
	
	// Команды  
	ДобавитьЭлементыВДеревоЗначений(СвойстваДока.Команды, 
	Строка, 
	"Команды");
	
	
	// Макеты          
	ДобавитьЭлементыВДеревоЗначений(СвойстваДока.Макеты, 
	Строка, 
	"Макеты");
	
		
	ЗначениеВДанныеформы(ДеревоЗн, Объект.МетаДерево);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ДобавитьЭлементыВДеревоЗначений(ИтерируемыеДанные, Родитель, ИмяСтроки)            

	РодительскаяСтрока 	 	= Родитель.Строки.Добавить();
	РодительскаяСтрока.Имя  = ИмяСтроки;
	
	Для Каждого Эл Из ИтерируемыеДанные Цикл
		
		Стр 		= РодительскаяСтрока.Строки.Добавить();
		Стр.Имя     = Эл.Имя;
		Стр.Синоним = Эл.Синоним;
						
	КонецЦикла;	
	
КонецПроцедуры
